root ::=
    thinkBlock "\n" reflectionLine "\n" reflectionLine "\n" reframeLine "\n" supportLine

# Support variables
word ::= [a-z']{1,20}
capitalizedWord ::= [A-Z][a-z']{0,19}
sentence ::= word (" " word){0, 20} "."
capitalizedSentence ::= capitalizedWord (" " word){0, 20} "."
paragraph ::= capitalizedSentence (" " capitalizedSentence){0, 4}

# Think block
thinkBlock ::=
    (
        "<think>\n" paragraph "\n1. "
        capitalizedSentence "\n2. "
        capitalizedSentence "\n3. "
        capitalizedSentence "\n4. "
        capitalizedSentence
        "\n</think>\n"
    )

# Reflection line
reflectionLine ::= bullet (youPattern | itIsPattern)

bullet ::= "* "

## You VERB SENTENCE
youPattern ::= "You " verbPhrase " " sentence
verbPhrase ::= simplePastVerb | presentTenseVerb | negationVerb
simplePastVerb ::= shareWord | emotionWord | communicationWord | struggleWordPast
shareWord ::= "shared" | "mentioned" | "described" | "talked about" | "noted" | "wrote about"
emotionWord ::= "felt" | "experienced" | "seemed" | "appeared" | "were feeling" | "sensed"
communicationWord ::= "expressed" | "conveyed" | "showed" | "revealed" | "communicated" | "indicated" | "voiced" | "articulated"
struggleWordPast ::= "struggled with" | "wrestled with" | "grappled with" | "were overwhelmed by" | "felt trapped in" | "were burdened by"
presentTenseVerb ::= "are " presentParticiple | "feel" | "seem" | "continue to" | "face" | "find yourself"
negationVerb ::= "aren't " presentParticiple | "don't " infinitiveVerb | "haven't been " presentParticiple
presentParticiple ::= "experiencing" | "feeling" | "working through" | "processing" | "dealing with" | "coping with" | "confronting" | "managing" | "navigating" | "facing" | "struggling with"
infinitiveVerb ::= "feel" | "see" | "think" | "believe" | "find" | "consider"

## It's ADJECTIVE that you VERB SENTENCE
itIsPattern ::= "It's " adjectivePhrase " that you " presentVerb " " sentence
adjectivePhrase ::= "understandable" | "common" | "normal" | "natural" | "challenging" | "difficult" | "hard" | "overwhelming" | "great" | "important" | "okay" | "valid" | "reasonable"
presentVerb ::= "feel" | "experience" | "face" | "encounter" | "struggle with" | "wrestle with" | "are working through"

# Reframe line
reframeLine ::= bullet reframeOpener " " sentence
reframeOpener ::= allowOpener | breakingOpener | celebrateOpener | changeOpener | communicateOpener | considerOpener |
                  embraceOpener | encourageOpener | engageOpener | exploreOpener | focusingOpener | hopeOpener |
                  planOpener | practiceOpener | reflectOpener | remindOpener | settingOpener | smallStepsOpener |
                  supportOpener | takingOpener |writingOpener

allowOpener ::= "Allowing yourself" | "Acknowledging" | "Feeling"
breakingOpener ::=  "Breaking free from" | "Breaking the cycle of" | "Breaking tasks into"
celebrateOpener ::= "Celebrating small" | "Appreciating" | "Honoring" | "Recognizing even small"
changeOpener ::= "Changing your" | "Shifting your" | "Transforming"
communicateOpener ::= "Having an open" | "Discussing" | "Talking openly about" | "Communicating"
considerOpener ::= "To better" | "To manage" | "To address" | "To cope with"
embraceOpener ::= "Embracing the" | "Welcoming"
encourageOpener ::= "Self-reflection can" | "Growth comes from" | "Learning to"
engageOpener ::= "Engaging with" | "Engaging in"
exploreOpener ::= "Exploring your" | "Trying" | "Discovering"
focusingOpener ::= "Focusing on" | "Prioritizing" | "Concentrating on"
hopeOpener ::= "Small steps can" | "Each day offers" | "Progress happens when"
planOpener ::= "Creating a plan" | "Scheduling" | "Organizing" | "Preparing"
practiceOpener ::= "Practicing self-care" | "Practicing mindfulness" | "Practicing relaxation"
reflectOpener ::= "Reflecting on" | "Recognizing" | "Acknowledging"
remindOpener ::= "Remembering that" | "Reminding yourself" | "Keeping in mind"
settingOpener ::= "Setting aside" | "Setting clear" | "Setting boundaries" | "Setting realistic"
smallStepsOpener ::= "Taking one day" | "Starting with" | "Beginning with"
supportOpener ::= "Support is available" | "Help is accessible"
takingOpener ::= "Taking small steps" | "Taking time" | "Taking breaks" | "Taking care"
writingOpener ::= "Writing down" | "Journaling"


# Support line
supportLine ::= bullet capitalizedSentence
